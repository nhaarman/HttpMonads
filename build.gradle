buildscript {
    ext.kotlinVersion = "1.3.70"

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("org.jetbrains.dokka:dokka-gradle-plugin:0.9.17")
    }
}

plugins {
    id("com.github.ben-manes.versions") version "0.28.0"
}

project.apply from: './gradle/scripts/git.gradle'

subprojects {

    project.apply from: "$rootDir/dependencies.gradle"

    repositories {
        mavenCentral()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    afterEvaluate {
        dokka {
            outputDirectory = javadoc.destinationDir.absolutePath
            includeNonPublic = false
        }
        javadoc.dependsOn(dokka)

        project.apply from: "$rootDir/publishing.gradle"

    }
}

println "${git.versionName()} (${git.commitCount()})"
